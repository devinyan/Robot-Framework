*** Settings ***
Documentation     AOP and AAC can use the same Test case.
Suite Setup       Open Homepage
Suite Teardown    Close Browser
Test Setup        Go to    ${asics_url}${login_url}
Force Tags        aop    aac    customer_registration
Resource          ../keywords/customer_registration.txt
Resource          ../keywords/setup.txt
Resource          ../variables/userdetails.txt

*** Test Cases ***
Invalid First Name
    [Setup]    Go to    ${asics_url}${login_url}
    Enter First Name    ${EMPTY}
    Enter Last Name    ${registration_lastname}
    Enter Email Address    nicole.toledo@asics.com
    Register
    Registration Error Message should appear    id=firstName.errors

Invalid Last Name
    [Setup]    Go to    ${asics_url}${login_url}
    Enter Last Name    ${EMPTY}
    Enter First Name    ${registration_firstname}
    Enter Email Address    nicole.toledo@asics.com
    Register
    Registration Error Message should appear    id=lastName.errors

Invalid Email Address
    Enter Email Address    whatever@
    Register
    Registration Error Message should appear    id=email.errors

Existing Email Address
    Enter First Name    ${registration_firstname}
    Enter Last Name    ${registration_lastname}
    Enter Email Address    nicole.toledo@asics.com
    Enter Password    asics123
    Enter Confirmed Password    asics123
    Register
    Registration Error Message should appear    id=email.errors
    Error Message should appear    ${error_general_message}

Weak Password
    Enter First Name    ${registration_firstname}
    Enter Last Name    ${registration_lastname}
    Enter Email Address    ${registration_email}
    Enter Password    1234
    Enter Confirmed Password    1234
    Register
    Registration Error Message should appear    id=pwd.errors
    Error Message should appear    ${error_general_message}

Newsletter checkbox should not be checked
    Run Keyword if    "${COUNTRY}" == 'AOP'    Email Checkbox shouldn't be checked
    Run Keyword IF    "${COUNTRY}" == 'AAC'    Email Checkbox should be checked

Passwords do not match
    Enter First Name    ${registration_firstname}
    Enter Last Name    ${registration_lastname}
    Enter Email Address    ${registration_email}
    Enter Password    123456
    Enter Confirmed Password    1234567
    Register
    Error Message should appear    ${error_password_no_match}
    Error Message should appear    ${error_general_message}

Successful Registration
    Enter First Name    ${registration_firstname}
    Enter Last Name    ${registration_lastname}
    ${dummyEmail}=    Generate Dummy Email
    Enter Email Address    ${dummyEmail}
    Enter Password    ${registration_password}
    Enter Confirmed Password    ${registration_password}
    Register
    Welcome Message should appear
